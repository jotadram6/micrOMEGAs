	SUBROUTINE RGESINVGM(PAR,IFAIL)

*   Subroutine to integrate the RGEs for all 21 soft terms
*   from MMESS down to Q2 = M_SUSY, through a call of the
*   subroutine ODEINTSGM that is part of the file integsgm.f
*   The same beta functions as for RGESGM are used.
*   If MAFLAG=-2 MS^2 at MMESS is taken from MSMES, XIS from MXSINP
*   If MAFLAG=-3 MS^2 at MMESS is taken from MXSINP, XIS from XISMES
*
*   It uses COMMON/SOFTINP, /INPPAR and /GMMESPAR for the soft terms,
*   COMMON/MESCOUP for the gauge/Yukawa couplings at MMES.
*
*   The output (soft terms at Q2) is written into PAR(*) and
*   COMMON/SUSYMH/MH1,MH2,MSS
*
*********************************************************************** 

	IMPLICIT NONE

	INTEGER IFAIL,OMGFLAG,MAFLAG,NN
	PARAMETER (NN=35)

	DOUBLE PRECISION PAR(*),EPS,X1,X2,Y(NN),PI,ALP1,ALP2,ALP3
	DOUBLE PRECISION g1s,g2s,g3s,HTOPS,HBOTS,HTAUS
	DOUBLE PRECISION Q2,COEF
	DOUBLE PRECISION G1MES,G2MES,G3MES,LMES,KMES,HTOPMES,
     C        HBOTMES,HTAUMES
	DOUBLE PRECISION MS,MC,MB,MBP,MT,MTAU,MMUON,MZ,MW
	DOUBLE PRECISION MH1,MH2,MSS
	DOUBLE PRECISION MSUSYEFF,MMESS,N5
	DOUBLE PRECISION AKINP,XIFINP,MXSINP,MUPINP,MSPINP,DELHINP
	DOUBLE PRECISION XIFMES,XISMES,MSMES,MUPMES,MSPMES,M3HMES
	DOUBLE PRECISION XIFSUSY,XISSUSY,MUPSUSY,MSPSUSY,M3HSUSY
	DOUBLE PRECISION M1INP,M2INP,M3INP,ALINP,ATINP,ABINP,
     C        ATAUINP,AMUINP,MHUINP,MHDINP,MQ3INP,MU3INP,MD3INP,
     C        MQINP,MUINP,MDINP,ML3INP,ME3INP,MLINP,MEINP
	DOUBLE PRECISION X,F1,F2,SP2

	COMMON/FLAGS/OMGFLAG,MAFLAG
	COMMON/SUSYCOUP/g1s,g2s,g3s,HTOPS,HBOTS,HTAUS
	COMMON/RENSCALE/Q2
	COMMON/MESCOUP/G1MES,G2MES,G3MES,LMES,KMES,HTOPMES,
     C        HBOTMES,HTAUMES	
	COMMON/SMSPEC/MS,MC,MB,MBP,MT,MTAU,MMUON,MZ,MW
	COMMON/SUSYMH/MH1,MH2,MSS
	COMMON/MESSPAR/MSUSYEFF,MMESS,N5
	COMMON/INPPAR/AKINP,XIFINP,MXSINP,MUPINP,MSPINP,DELHINP
	COMMON/GMMESPAR/XIFMES,XISMES,MSMES,MUPMES,MSPMES,M3HMES
	COMMON/GMSUSYPAR/XIFSUSY,XISSUSY,MUPSUSY,MSPSUSY,M3HSUSY
	COMMON/SOFTINP/M1INP,M2INP,M3INP,ALINP,ATINP,ABINP,
     C        ATAUINP,AMUINP,MHUINP,MHDINP,MQ3INP,MU3INP,MD3INP,
     C        MQINP,MUINP,MDINP,ML3INP,ME3INP,MLINP,MEINP
	
	EXTERNAL DERIVSSGM,RKQSSGM

	EPS=1.D-8
	PI=4.D0*DATAN(1.D0)
	COEF=1.D0/(16.D0*PI**2)

* Definition of the couplings squared Y(I) at MMESS

	Y(1)=G1MES
	Y(2)=G2MES
	Y(3)=G3MES
	Y(4)=LMES
* NOTE: KMES = KAPPA, NOT KAPPA**2
	Y(5)=KMES
	Y(6)=HTOPMES
	Y(7)=HBOTMES
	Y(8)=HTAUMES

* Corrections higher order in X=MSUSYEFF/MMESS:

	X=MSUSYEFF/MMESS
	IF(X.GE.1.D-7) THEN
	  IF(X.LT.1.D0) THEN
	    F1=((1.D0+X)*DLOG(1.D0+X)+(1.D0-X)*DLOG(1.D0-X))/X**2
	    F2=(1.D0+X)/X**2*(DLOG(1.D0+X)-2.D0*SP2(X/(1.D0+X))
     C         +SP2(2.D0*X/(1.D0+X))/2.D0)
     C         +(1.D0-X)/X**2*(DLOG(1.D0-X)-2.D0*SP2(-X/(1.D0-X))
     C         +SP2(-2.D0*X/(1.D0-X))/2.D0)  
	  ELSE
	    F1=0.D0
	    F2=0.D0
	  ENDIF
	ELSE
	  F1=1.D0
	  F2=1.D0
	ENDIF

* (SP2 = Li_2  is defined in the subroutine bsg.f)
	
* Input values for the soft terms at MMESS:

	ALP1=G1MES/(4.D0*PI)
	ALP2=G2MES/(4.D0*PI)
	ALP3=G3MES/(4.D0*PI)
	
	Y(9)=5.D0/3.D0*ALP1*N5*MSUSYEFF*F1/(4.D0*PI)
	Y(10)=ALP2*N5*MSUSYEFF*F1/(4.D0*PI)
	Y(11)=ALP3*N5*MSUSYEFF*F1/(4.D0*PI)
	IF(AKINP.EQ.0.D0) AKINP=EPS
	Y(12)=AKINP/3.D0
	Y(13)=AKINP
	Y(14)=0.D0
	Y(15)=0.D0
	Y(16)=0.D0
	Y(17)=COEF*((5.D0/6.D0*ALP1**2+3.D0/2.D0*ALP2**2)*N5*F2
     C	     -COEF*LMES*DELHINP)*MSUSYEFF**2
	Y(18)=COEF*((5.D0/6.D0*ALP1**2+3.D0/2.D0*ALP2**2)*N5*F2
     C	     -COEF*LMES*DELHINP)*MSUSYEFF**2
	IF(MAFLAG.EQ.-2)THEN
	 Y(19)=MSMES
	ELSE
	 Y(19)=MXSINP
	ENDIF
	Y(20)=COEF*(5.D0/54.D0*ALP1**2+3.D0/2.D0*ALP2**2
     C	     +8.D0/3.D0*ALP3**2)*F2*N5*MSUSYEFF**2
	Y(21)=COEF*(40.D0/27.D0*ALP1**2
     C	     +8.D0/3.D0*ALP3**2)*F2*N5*MSUSYEFF**2
	Y(22)=COEF*(10.D0/27.D0*ALP1**2
     C	     +8.D0/3.D0*ALP3**2)*F2*N5*MSUSYEFF**2
	Y(23)=COEF*(5.D0/54.D0*ALP1**2+3.D0/2.D0*ALP2**2
     C	     +8.D0/3.D0*ALP3**2)*F2*N5*MSUSYEFF**2
	Y(24)=COEF*(40.D0/27.D0*ALP1**2
     C	     +8.D0/3.D0*ALP3**2)*F2*N5*MSUSYEFF**2
	Y(25)=COEF*(10.D0/27.D0*ALP1**2
     C	     +8.D0/3.D0*ALP3**2)*F2*N5*MSUSYEFF**2
	Y(26)=COEF*(5.D0/6.D0*ALP1**2
     C	     +3.D0/2.D0*ALP2**2)*F2*N5*MSUSYEFF**2
	Y(27)=COEF*(10.D0/3.D0*ALP1**2)*F2*N5*MSUSYEFF**2
	Y(28)=COEF*(5.D0/6.D0*ALP1**2
     C	     +3.D0/2.D0*ALP2**2)*F2*N5*MSUSYEFF**2
	Y(29)=COEF*(10.D0/3.D0*ALP1**2)*F2*N5*MSUSYEFF**2
	Y(30)=XIFINP
	IF(MAFLAG.EQ.-2)THEN
	 Y(31)=MXSINP
	ELSE
	 Y(31)=XISMES
	ENDIF
	Y(32)=MUPINP
	Y(33)=MSPINP
	IF(Y(32).NE.0.D0 .AND. Y(33).EQ.0.D0) Y(33)=EPS
	Y(34)=0.D0
	Y(35)=0.D0
 
* Save the required input values for soft masses and couplings in 
* COMMON/SOFTINP

	M1INP=Y(9)
	M2INP=Y(10)
	M3INP=Y(11)
	ALINP=Y(12)
	AKINP=Y(13)
	ATINP=Y(14)
	ABINP=Y(15)
	ATAUINP=Y(16)
	MHUINP=Y(17)
	MHDINP=Y(18)
	MQ3INP=Y(20)
	MU3INP=Y(21)
	MD3INP=Y(22)
	MQINP=Y(23)
	MUINP=Y(24)
	MDINP=Y(25)
	ML3INP=Y(26)
	ME3INP=Y(27)
	MLINP=Y(28)
	MEINP=Y(29)
	AMUINP=Y(35)

	X1=COEF*DLOG(MMESS**2/Q2)
	X2=0.D0

* NOTE: Y(5) = KAPPA, NOT KAPPA**2

	CALL ODEINTSGM(Y,NN,X1,X2,EPS,DERIVSSGM,RKQSSGM,IFAIL)	

	IF(IFAIL.NE.0)THEN
	 IFAIL=13
	 RETURN
	ENDIF

* SOFT TERMS AT THE SUSY SCALE

	PAR(5)=Y(12)
	PAR(6)=Y(13)
	PAR(7)=Y(20)
	PAR(8)=Y(21)
	PAR(9)=Y(22)
	PAR(10)=Y(26)
	PAR(11)=Y(27)
	PAR(12)=Y(14)
	PAR(13)=Y(15)
	PAR(14)=Y(16)
	PAR(15)=Y(23)
	PAR(16)=Y(24)
	PAR(17)=Y(25)
	PAR(18)=Y(28)
	PAR(19)=Y(29)
	PAR(20)=Y(9)
	PAR(21)=Y(10)
	PAR(22)=Y(11)
	PAR(24)=Y(35)

* MH1, MH2 AND MSS at Q2 are stored in COMMON/SUSYMH:
	
	MH1=Y(17)
	MH2=Y(18)
	MSS=Y(19)
	
* GMSB parameters at Q2, stored in COMMON/GMSUSYPAR:

	XIFSUSY=Y(30)
	XISSUSY=Y(31)
	MUPSUSY=Y(32)
	MSPSUSY=Y(33)
	M3HSUSY=Y(34)
	
	END
