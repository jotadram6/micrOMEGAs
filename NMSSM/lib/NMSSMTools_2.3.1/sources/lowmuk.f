	SUBROUTINE LOWMUK(PAR,CHECK)

**********************************************************************	
* Subroutine to compute mu, k and mss at the scale QSTSB
*
* k at QSTSB is stored in KQ, k at Q2 in PAR(2)
* mu at QSTSB is stored in MUQ, mu at Q2 is stored in PAR(4)
* MSS is stored in COMMON/QMHIGGS
* MA at QSTSB is stored in PAR(23)
*
**********************************************************************

	IMPLICIT NONE

	DOUBLE PRECISION PAR(*),CHECK,SIGMU,pi,At,Ab,COEF
	DOUBLE PRECISION B,MUOLD,KOLD,MSSOLD
	DOUBLE PRECISION mst1,mst2,s2t,msb1,msb2,s2b,XT,XB
	DOUBLE PRECISION ct,fmt1,fmt2,fmt,gmt
	DOUBLE PRECISION cb,fmb1,fmb2,fmb,gmb
	DOUBLE PRECISION MH1,MH2,MSS,QSTSB
	DOUBLE PRECISION ALSMZ,ALEMMZ,GF,g1,g2,S2TW
	DOUBLE PRECISION MS,MC,MB,MBP,MT,MTAU,MMUON,MZ,MW
	DOUBLE PRECISION RT1,RT2,RB1,RB2,AM,BM,CM,DETM
	DOUBLE PRECISION G1Q,G2Q,GQ,ALSQ
	DOUBLE PRECISION ZHU,ZHD,ZS,H1Q,H2Q,TANBQ
	DOUBLE PRECISION HTQ,HBQ,MTOPQ,MBOTQ
	DOUBLE PRECISION LQ,KQ,ALQ,AKQ,MUQ,NUQ
	DOUBLE PRECISION g1s,g2s,g3s,HTOPS,HBOTS,HTAUS
	DOUBLE PRECISION LS2,KS2,HTOPS2,HBOTS2,HTAUS2
	DOUBLE PRECISION M1,M2,M3,ATAU,MQ3,MU3,MD3
	DOUBLE PRECISION MQ,MU,MD,ML3,ME3,ML,ME,AL
	DOUBLE PRECISION Q2,MH1X,MH2X,MSSX
	DOUBLE PRECISION LQSTSB,LM1QSTSB,LM2QSTSB
	DOUBLE PRECISION LM3QSTSB,LMQ3QSTSB,LMU3QSTSB
	DOUBLE PRECISION LMD3QSTSB,LMQQSTSB,LMUQSTSB,LMDQSTSB
	DOUBLE PRECISION LML3QSTSB,LME3QSTSB,LMLQSTSB,LMEQSTSB
	DOUBLE PRECISION RL,RTOP,RBOT,RTAU,RG,ANOMQSTSB,MUFAIL

	COMMON/GAUGE/ALSMZ,ALEMMZ,GF,g1,g2,S2TW
	COMMON/SMSPEC/MS,MC,MB,MBP,MT,MTAU,MMUON,MZ,MW
	COMMON/STSBSCALE/QSTSB
	COMMON/RADCOR/mst1,mst2,s2t,msb1,msb2,s2b,XT,XB
	COMMON/QGAUGE/G1Q,G2Q,GQ,ALSQ
	COMMON/QHIGGS/ZHU,ZHD,ZS,H1Q,H2Q,TANBQ
	COMMON/QQUARK/HTQ,HBQ,MTOPQ,MBOTQ
	COMMON/QNMPAR/LQ,KQ,ALQ,AKQ,MUQ,NUQ
        COMMON/SIGMU/SIGMU
	COMMON/SUSYCOUP/g1s,g2s,g3s,HTOPS,HBOTS,HTAUS
	COMMON/RENSCALE/Q2
	COMMON/QMHIGGS/MH1X,MH2X,MSSX
	COMMON/SUSYMH/MH1,MH2,MSS
	COMMON/DETM/DETM
	COMMON/MUFAIL/MUFAIL

	pi=4.D0*DATAN(1.D0)
	COEF=1.D0/(16.D0*PI**2)
	
	!PRINT*,"CALL LOWMUK"
	!PRINT*,""

*  Store the previous value of MU, K and MS for CHECK:

	MUOLD=PAR(4)
	KOLD=PAR(2)
	MSSOLD=MSS

*  MH1, MH2 are read in from COMMON/SUSYMH at the scale Q2.
*  Here we integrate MH1, MH2 from Q2 to QSTSB:

	LS2=PAR(1)**2
	KS2=PAR(2)**2
	HTOPS2=HTOPS**2
	HBOTS2=HBOTS**2
	HTAUS2=HTAUS**2

	MU=PAR(4)
	M1=PAR(20)
	M2=PAR(21)
	M3=PAR(22)
	At=PAR(12)
	Ab=PAR(13)
	ATAU=PAR(14)
	AL=PAR(5)

	MQ3=PAR(7)
	MU3=PAR(8)
	MD3=PAR(9)
	ML3=PAR(10)
	ME3=PAR(11)
	MQ=PAR(15)
	MU=PAR(16)
	MD=PAR(17)
	ML=PAR(18)
	ME=PAR(19)

*  Useful logarithms:

	LQSTSB=DLOG(Q2/QSTSB)
	LM1QSTSB=DLOG(Q2/MAX(M1**2,QSTSB))
	LM2QSTSB=DLOG(Q2/MAX(M2**2,QSTSB))
	LM3QSTSB=DLOG(Q2/MAX(M3**2,QSTSB))
	LMQ3QSTSB=DLOG(Q2/MAX(MQ3,QSTSB))
	LMU3QSTSB=DLOG(Q2/MAX(MU3,QSTSB))
	LMD3QSTSB=DLOG(Q2/MAX(MD3,QSTSB))
	LMQQSTSB=DLOG(Q2/MAX(MQ,QSTSB))
	LMUQSTSB=DLOG(Q2/MAX(MU,QSTSB))
	LMDQSTSB=DLOG(Q2/MAX(MD,QSTSB))
	LML3QSTSB=DLOG(Q2/MAX(ML3,QSTSB))
	LME3QSTSB=DLOG(Q2/MAX(ME3,QSTSB))
	LMLQSTSB=DLOG(Q2/MAX(ML,QSTSB))
	LMEQSTSB=DLOG(Q2/MAX(ME,QSTSB))

	ANOMQSTSB=G1S*(-MH2*LQSTSB+MQ3*LMQ3QSTSB
     C          -2.D0*MU3*LMU3QSTSB
     C          +MD3*LMD3QSTSB+2.D0*(MQ*LMQQSTSB
     C          -2.D0*MU*LMUQSTSB+MD*LMDQSTSB)
     C          +ME3*LME3QSTSB-ML3*LML3QSTSB
     C          +2.D0*(ME*LMEQSTSB-ML*LMLQSTSB))

	RTOP=HTOPS2*(MQ3*LMQ3QSTSB+MU3*LMU3QSTSB
     C       +AT**2*LQSTSB)

  	RBOT=HBOTS2*(MH2*LQSTSB+MQ3*LMQ3QSTSB+MD3*LMD3QSTSB
     C       +AB**2*LQSTSB) 

	RTAU=HTAUS2*(MH2*LQSTSB+ML3*LML3QSTSB+ME3*LME3QSTSB
     C       +ATAU**2*LME3QSTSB)

	RL=LS2*(MH2+MSS+AL**2)*LQSTSB

	RG=G1S*M1**2*LM1QSTSB+3.D0*G2S*M2**2*LM2QSTSB

*   Running MH1, MH2 from Q2 to QSTSB:

        MH1X=MH1*(QSTSB/Q2)**(COEF*(LS2+3.D0*HTOPS2+G1S/2.D0))
     C      -COEF*(RL+3.D0*RTOP-RG+ANOMQSTSB/2.D0)

        MH2X=MH2-COEF*(RL+3.D0*RBOT+RTAU-RG-ANOMQSTSB/2.D0
     C	    +(LS2-G1S/2.D0)*MH1*LQSTSB)


* (S)top/(S)/bottom loop corrections to the minimization equations:

	ct=3.D0*htq**2*COEF
	fmt1=mst1*(DLOG(mst1/QSTSB)-1.D0)
	fmt2=mst2*(DLOG(mst2/QSTSB)-1.D0)
	fmt=mtopq**2*(DLOG(mtopq**2/QSTSB)-1.D0)
	IF(mst1-mst2.NE.0.D0)THEN
	 gmt=(fmt2-fmt1)/(mst2-mst1)
	ELSE
	 gmt=DLOG(mst1/QSTSB)
	ENDIF

	cb=3.D0*hbq**2*COEF
	fmb1=msb1*(DLOG(msb1/QSTSB)-1.D0)
	fmb2=msb2*(DLOG(msb2/QSTSB)-1.D0)
	fmb=mbotq**2*(DLOG(mbotq**2/QSTSB)-1.D0)
	IF(msb1-msb2.NE.0.D0)THEN
	 gmb=(fmb2-fmb1)/(msb2-msb1)
	ELSE
	 gmb=DLOG(msb1/QSTSB)
	ENDIF
	
	RT1=CT*(FMT1+FMT2-2.D0*FMT+AT*XT*GMT)
	RT2=CT*XT*GMT
	RB1=CB*(FMB1+FMB2-2.D0*FMB+AB*XB*GMB)
	RB2=CB*XB*GMB

* Prepare the computation of MU:

	am=tanbq-1.D0/tanbq
	bm=rt2-rb2
	cm=gq/2.D0*(H1Q**2+H2Q**2)*(tanbq-1.D0/tanbq)
     c     +(rt1+MH1X)*tanbq-(rb1+MH2X)/tanbq
     
        detm=bm**2-4.D0*am*cm

*  MU at QSTSB is computed here:

	IF(DETM.GE.0.D0)THEN
	 MUQ=(-BM+SIGMU*DSQRT(DETM))/(2.D0*AM)
	ELSE
	 !PRINT*,"DETM < 0"
	 !PRINT*,""
	 MUQ=MUFAIL
	ENDIF

	IF(DABS(MUQ).LT.DABS(MUFAIL)) THEN
	 !PRINT*,"DETM = 0"
	 !PRINT*,""
	 DETM=0.D0
	 MUQ=MUFAIL
	ENDIF

*  PAR(4) = MU at Q2
*  (MU at QSTSB = MUQ is stored in COMMON/QNMPAR)	

	PAR(4)=MUQ/(1.D0+COEF/2.D0*(2.D0*LQ**2+3.D0*(HTQ**2+HBQ**2)
     C	       +(MTAU/H2Q)**2-G1Q-3.D0*G2Q)*DLOG(QSTSB/Q2))

*  K at QSTSB

	B=TANBQ/((1.D0+TANBQ**2)*MUQ)*(MH1X+MH2X+2.D0*MUQ**2
     C	  +LQ**2*(H1Q**2+H2Q**2)+RT1+RB1-MUQ*(RT2*TANBQ+RB2/TANBQ))
	KQ=LQ*(B-ALQ)/MUQ

*  PAR(2) = K at Q2
*  (K at QSTSB = KQ is stored in COMMON/QNMPAR)	

	PAR(2)=KQ/(1.D0+3.D0*COEF*(LQ**2+KQ**2)*DLOG(QSTSB/Q2))

*  NU at QSTSB stored in COMMON/QNMPAR

	NUQ=MUQ*KQ/LQ


* MSSX = MS at QSTSB, stored in COMMON/QMHIGGS:

	MSSX=-LQ**2*(H1Q**2+H2Q**2) - 2.D0*NUQ**2
     C	     +LQ**2*H1Q*H2Q/MUQ*(ALQ+2.D0*NUQ) - NUQ*AKQ
     C	     +ct*LQ**2*H1Q*H2Q/MUQ*Xt*gmt
     C	     +cb*LQ**2*H1Q*H2Q/MUQ*Xb*gmb

*  Run MS from QSTSB to Q2, stored in COMMON/SUSYMH:

	MSS=MSSX+COEF*(2.D0*LQ**2*(MH1X+MH2X+MSSX+ALQ**2)
     C      +2.D0*KQ**2*(3.D0*MSSX+AKQ**2))*DLOG(Q2/QSTSB)

*  CHECK of MU, K and MS against the previous value

	CHECK=MAX(((MUOLD-PAR(4))/MAX(DABS(PAR(4)),DABS(MUOLD)))**2,
     C     ((KOLD-PAR(2))/MAX(DABS(PAR(2)),DABS(KOLD)))**2,
     C	   ((MSSOLD-MSS)/MAX(DABS(MSS),DABS(MSSOLD)))**2)

*   Approximate value for the MSSM-like CP odd Higgs mass squared MA:

	PAR(23)=DSQRT(MAX(MUQ*B*(tanbQ+1.D0/tanbQ),0.D0))

	!PRINT*,"MUS =",PAR(4)
	!PRINT*,"MSS =",MSS
	!PRINT*,"KS =",PAR(2)
	!PRINT*,"MA =",PAR(23)
	!PRINT*,""
	!PRINT*,"CHECK =",CHECK
	!PRINT*,""
	!PRINT*,""

	END
