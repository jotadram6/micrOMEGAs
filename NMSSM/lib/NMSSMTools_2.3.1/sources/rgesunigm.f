	SUBROUTINE RGESUNIGM(PAR,IFAIL,MESTEST)

*   Subroutine to integrate the RGEs 
*   from the SUSY scale Q2 up to MMESS, through a call of the
*   subroutine ODEINTSGM that is part of the file integsgm.f
*
*   Present accuracy: 
*   Gauginos: two loops
*   Trilinear couplings and scalar masses: two loops, 
*   except for terms ~lambda**2 and kappa**2
*
*   The gauge/Yukawa couplings at Q2 are read in from
*   COMMON/SUSYCOUP, initialized in RGESGM.
*
*   The soft terms at Q2 are read in from PAR(*).
*   MH1, MH2, MSS are read in from COMMON/SUSYMH.
*
*   PURPOSE: Once the gauge/Yukawa couplings at Q2 have been computed
*   in RGESGM (including SUSY thresholds), the agreement of the soft
*   terms at MMESS with the inputs is tested via MESTEST.
*
*   IF MAFLAG=-2 MS**2 at MMESS is computed here
*   IF MAFLAG=-3 XIS at MMESS is computed here
*
* NOTE: Y(5)=kappa=PAR(2), NOT kappa^2, in the GMSB version, since the
* sign of kappa matters in the RGEs (lambda is assumed to be positive)
*
*********************************************************************** 

	IMPLICIT NONE

	INTEGER IFAIL,OMGFLAG,MAFLAG,NN
	PARAMETER (NN=35)

	DOUBLE PRECISION PAR(*),EPS,X1,X2,Y(NN),PI,COEF
	DOUBLE PRECISION g1s,g2s,g3s,HTOPS,HBOTS,HTAUS,Q2
	DOUBLE PRECISION MS,MC,MB,MBP,MT,MTAU,MMUON,MZ,MW
	DOUBLE PRECISION M1INP,M2INP,M3INP,ALINP,ATINP,ABINP,
     C        ATAUINP,AMUINP,MHUINP,MHDINP,MQ3INP,MU3INP,MD3INP,
     C        MQINP,MUINP,MDINP,ML3INP,ME3INP,MLINP,MEINP
     	DOUBLE PRECISION M1MES,M2MES,M3MES,ALMES,AKMES,ATMES,ABMES,
     C        ATAUMES,AMUMES,MHUMES,MHDMES,MQ3MES,MU3MES,MD3MES,
     C        MQMES,MUMES,MDMES,ML3MES,ME3MES,MLMES,MEMES
        DOUBLE PRECISION MH1,MH2,MSS,MESTEST
	DOUBLE PRECISION XIFSUSY,XISSUSY,MUPSUSY,MSPSUSY,M3HSUSY
	DOUBLE PRECISION G1MES,G2MES,G3MES,LMES,KMES,HTOPMES,
     C        HBOTMES,HTAUMES
	DOUBLE PRECISION AKINP,XIFINP,MXSINP,MUPINP,MSPINP,DELHINP
	DOUBLE PRECISION XIFMES,XISMES,MSMES,MUPMES,MSPMES,M3HMES
	DOUBLE PRECISION MSUSYEFF,MMESS,N5
    
	COMMON/FLAGS/OMGFLAG,MAFLAG
	COMMON/SUSYCOUP/g1s,g2s,g3s,HTOPS,HBOTS,HTAUS
	COMMON/SMSPEC/MS,MC,MB,MBP,MT,MTAU,MMUON,MZ,MW
	COMMON/RENSCALE/Q2
	COMMON/SOFTINP/M1INP,M2INP,M3INP,ALINP,ATINP,ABINP,
     C        ATAUINP,AMUINP,MHUINP,MHDINP,MQ3INP,MU3INP,MD3INP,
     C        MQINP,MUINP,MDINP,ML3INP,ME3INP,MLINP,MEINP
 	COMMON/SOFTMES/M1MES,M2MES,M3MES,ALMES,AKMES,ATMES,ABMES,
     C        ATAUMES,AMUMES,MHUMES,MHDMES,MQ3MES,MU3MES,MD3MES,
     C        MQMES,MUMES,MDMES,ML3MES,ME3MES,MLMES,MEMES
	COMMON/GMSUSYPAR/XIFSUSY,XISSUSY,MUPSUSY,MSPSUSY,M3HSUSY
	COMMON/SUSYMH/MH1,MH2,MSS
	COMMON/MESCOUP/G1MES,G2MES,G3MES,LMES,KMES,HTOPMES,
     C        HBOTMES,HTAUMES	
	COMMON/MESSPAR/MSUSYEFF,MMESS,N5
	COMMON/INPPAR/AKINP,XIFINP,MXSINP,MUPINP,MSPINP,DELHINP
	COMMON/GMMESPAR/XIFMES,XISMES,MSMES,MUPMES,MSPMES,M3HMES

	EXTERNAL DERIVSSGM,RKQSSGM

	EPS=1.D-8
	PI=4.D0*DATAN(1.D0)
	COEF=1.D0/(16.D0*PI**2)
	
* Definition of the couplings squared Y(I) at Q2=M_SUSY

	Y(1)=g1s
	Y(2)=g2s
	Y(3)=g3s
	Y(4)=PAR(1)**2
* NOTE: Y(5)=K, NOT K**2
	Y(5)=PAR(2)
	Y(6)=HTOPS**2
	Y(7)=HBOTS**2
	Y(8)=HTAUS**2

* Definition of the soft terms Y(I) at Q2=M_SUSY

	Y(9)=PAR(20)
	Y(10)=PAR(21)
	Y(11)=PAR(22)
	Y(12)=PAR(5)
	Y(13)=PAR(6)
	Y(14)=PAR(12)
	Y(15)=PAR(13)
	Y(16)=PAR(14)
	Y(17)=MH1
	Y(18)=MH2
	Y(19)=MSS
	Y(20)=PAR(7)
	Y(21)=PAR(8)
	Y(22)=PAR(9)
	Y(23)=PAR(15)
	Y(24)=PAR(16)
	Y(25)=PAR(17)
	Y(26)=PAR(10)
	Y(27)=PAR(11)
	Y(28)=PAR(18)
	Y(29)=PAR(19)
	Y(30)=XIFSUSY
	Y(31)=XISSUSY
	Y(32)=MUPSUSY
	Y(33)=MSPSUSY
	Y(34)=M3HSUSY
	Y(35)=PAR(24)
	
	X1=0.D0
	X2=COEF*DLOG(MMESS**2/Q2)

	CALL ODEINTSGM(Y,NN,X1,X2,EPS,DERIVSSGM,RKQSSGM,IFAIL)

	IF(IFAIL.NE.0)THEN
	 IFAIL=12
	 RETURN
	ENDIF

* MESTEST:

	MESTEST=0.D0
	MESTEST=MESTEST+(Y(9)-M1INP)**2/(1.D2+M1INP**2)
	MESTEST=MESTEST+(Y(10)-M2INP)**2/(1.D2+M2INP**2)
	MESTEST=MESTEST+(Y(11)-M3INP)**2/(1.D2+M3INP**2)
	MESTEST=MESTEST+(Y(12)-ALINP)**2/(1.D2+ALINP**2)
	MESTEST=MESTEST+(Y(13)-AKINP)**2/(1.D2+AKINP**2)
	MESTEST=MESTEST+(Y(14)-ATINP)**2/(1.D2+ATINP**2)
	MESTEST=MESTEST+(Y(15)-ABINP)**2/(1.D2+ABINP**2)
	MESTEST=MESTEST+(Y(16)-ATAUINP)**2/(1.D2+ATAUINP**2)
	MESTEST=MESTEST+(Y(17)-MHUINP)**2/(1.D4+MHUINP**2)
	MESTEST=MESTEST+(Y(18)-MHDINP)**2/(1.D4+MHDINP**2)
	IF(MAFLAG.EQ.-3)THEN
	 MESTEST=MESTEST+(Y(19)-MXSINP)**2/(1.D4+MXSINP**2)
	ENDIF
	MESTEST=MESTEST+(Y(20)-MQ3INP)**2/(1.D4+MQ3INP**2)
	MESTEST=MESTEST+(Y(21)-MU3INP)**2/(1.D4+MU3INP**2)
	MESTEST=MESTEST+(Y(22)-MD3INP)**2/(1.D4+MD3INP**2)
	MESTEST=MESTEST+(Y(23)-MQINP)**2/(1.D4+MQINP**2)
	MESTEST=MESTEST+(Y(24)-MUINP)**2/(1.D4+MUINP**2)
	MESTEST=MESTEST+(Y(25)-MDINP)**2/(1.D4+MDINP**2)
	MESTEST=MESTEST+(Y(26)-ML3INP)**2/(1.D4+ML3INP**2)
	MESTEST=MESTEST+(Y(27)-ME3INP)**2/(1.D4+ME3INP**2)
	MESTEST=MESTEST+(Y(28)-MLINP)**2/(1.D4+MLINP**2)
	MESTEST=MESTEST+(Y(29)-MEINP)**2/(1.D4+MEINP**2)
	MESTEST=MESTEST+(Y(30)-XIFINP)**2/(1.D4+XIFINP**2)
	IF(MAFLAG.EQ.-2)THEN
	 MESTEST=MESTEST+(Y(31)-MXSINP)**2/(1.D4+MXSINP**2)
	ENDIF
	MESTEST=MESTEST+(Y(32)-MUPINP)**2/(1.D2+MUPINP**2)
	MESTEST=MESTEST+(Y(33)-MSPINP)**2/(1.D4+MSPINP**2)
	MESTEST=MESTEST+(Y(34))**2/1.D4
	MESTEST=MESTEST+(Y(35)-AMUINP)**2/(1.D2+AMUINP**2)
	
* Couplings at the messenger scale

	G1MES=Y(1)
	G2MES=Y(2)
	G3MES=Y(3)
	LMES=Y(4)
	KMES=Y(5)
	HTOPMES=Y(6)
	HBOTMES=Y(7)
	HTAUMES=Y(8)
	
* Soft terms at the messenger scale

	M1MES=Y(9)
	M2MES=Y(10)
	M3MES=Y(11)
	ALMES=Y(12)
	AKMES=Y(13)
	ATMES=Y(14)
	ABMES=Y(15)
	ATAUMES=Y(16)
	MHUMES=Y(17)
	MHDMES=Y(18)
	MSMES=Y(19)
	MQ3MES=Y(20)
	MU3MES=Y(21)
	MD3MES=Y(22)
	MQMES=Y(23)
	MUMES=Y(24)
	MDMES=Y(25)
	ML3MES=Y(26)
	ME3MES=Y(27)
	MLMES=Y(28)
	MEMES=Y(29)
	XIFMES=Y(30)
	XISMES=Y(31)
	MUPMES=Y(32)
	MSPMES=Y(33)
	M3HMES=Y(34)
	AMUMES=Y(35)
	
	END
